module pattern-matching

imports

  libstratego-gpp
  libspoofax/stratego/debug
  libstratego-lib
  src-gen/signatures/-
  nabl2/api
  pp
  desugar/desugar
  minifsharp
  src-gen/nabl2/static-semantics
  compile/compile
  compile/helper-funcs

signature constructors
	
	Output: String * String -> Term
	
rules
	exp-to-cil: (match@Match(e, matches), context@(func, wildcardDefs)) -> result with
		namedWildcards := <getNamedWildcars> matches;
		allWildcards   := <union> (wildcardDefs, namedWildcards);
		endOfMatch     := <newname> "endOfMatch_";
		ty             := <nabl2-get-ast-type> e;
		a              := <exp-to-cil> (e, context);	
		b              := <mapconcat(\x -> <rule-to-cil> (ty, x, endOfMatch, (func, allWildcards))\)> matches;
		nomatch        := [Pop(), LoadString(""), Ldc("0"), Ldc("0"), NewObj(ClassType(Module("FSharp.Core"), ClassName("Microsoft.FSharp.Core.MatchFailureException"), NonGeneric()), [StringType(), IntType(), IntType()]), Throw()];
		result         := <concat> [a, b, nomatch, [Label(endOfMatch)]]
	
	rule-to-cil: (ty, Rule(lhs, rhs), endOfMatch, context) -> result with
		endOfRule  := <newname> "endOfRule_";
		comparison := <ruleComparison-to-cil> (ty, lhs, endOfRule, context);
		b          := <exp-to-cil> (rhs, context);
		result     := <concat> [comparison, b, [Br(LabelRef(endOfMatch)), Label(endOfRule)]]
	
	
	
	// Convention for ruleComparison-to-cil: Caller pushes exp onto the stack. If match, leave nothing on stack. Otherwise, leave exp on stack.
	

	ruleComparison-to-cil: (_, Wildcard(), endOfRule, context) -> [Pop()]
	ruleComparison-to-cil: z@(_, lhs@Identifier(subtype), endOfRule, context@(func,wildcards)) -> result with
		if <getOrigin; ?TypeConstIdentifier(_)> lhs then
			result := <typeConstIdentifier-to-cil> z
		else
			result := <namedWildcard-to-cil> z
		end
	
	ruleComparison-to-cil: (Bool(), a, b, c) -> <ruleComparison-to-cil> (Int(), a, b, c) // bool is handled the same as ints	
	ruleComparison-to-cil: (Int(), lhs, endOfRule, context) -> result with
		a      := <concat> [[Dup()], <exp-to-cil> (lhs, context), [Bne(LabelRef(endOfRule))]];
		result := <concat> [a, [Pop()]]
	
	
	ruleComparison-to-cil: (tupleTy@Tuple(l1), lhs@PatternTuple(l2), endOfRule, context) -> result with 
		end             := <newname>"tupleMatch_end_";
		noMatch         := <newname> "tupleMatch_nomatch_";
		match           := <newname> "tupleMatch_match_";
		elemComparisons := <zip;number(id); mapconcat(\((elemTy,lhsElem), idx) -> <compareTupleElem> (elemTy, lhsElem, idx, tupleTy, lhs, noMatch, context)\)> (l1, l2);
		result          := <concat> [elemComparisons, [Pop(),Br(end), Label(noMatch), Br(endOfRule), Label(end)]] 
	
	ruleComparison-to-cil: (ty@List(x), lhs, endOfRule, context@(func,wildcards)) -> result with
		listModuleClass := ClassType(Module("FSharp.Core"), ClassName("Microsoft.FSharp.Collections.ListModule"), NonGeneric());
		lengthRetTy     := ClassType(Module("FSharp.Core"), ClassName("Microsoft.FSharp.Collections.FSharpList"), GenericClassTypeArgs("1", [ArgType2("0")]));
		getListLen      := [Call(Static(), IntType(), listModuleClass, "Length", Some(GenericTypeIndicator([<type-to-cil> x])), [lengthRetTy])]; 
		pushLhsLen      := [Ldc(<getPatternListLength;int-to-string> lhs)];
		if <endsWithWildcard> lhs then
			comparison := [Blt(LabelRef(endOfRule))]
		else
			comparison := [Bne(LabelRef(endOfRule))] 
		end;
		elemComparisons := <compareListItems> (lhs, (ty, endOfRule, context));
		result          := <concat> [
			[ Dup()], 
			getListLen,
			pushLhsLen,
			comparison,
			elemComparisons]
	
	ruleComparison-to-cil: (ty@UnionType(x), lhs@TypeConstruct(subtype, pat), endOfRule, context@(func,wildcards)) -> result with
		noMatch         := <newname> "unionMatch_nomatch_";
		match           := <newname> "unionMatch_match_";
		// check whether  lhs is of same subtype
		baseTy := ClassType(DefaultModule(), ClassName(<concat-strings> ["Test1/", x]), NonGeneric());
		subClass := ClassType(DefaultModule(), ClassName(<concat-strings> ["Test1/", subtype]), NonGeneric());
		valTy := <nabl2-get-ast-type;type-to-cil> pat;
		q := [
			Dup(),
			Call(Static(), IntType(), subClass, "GetTag", None(), []),
			Call(Instance(), BoolType(), baseTy, "EqualTypes", None(), [IntType()]),
			BrFalse(LabelRef(endOfRule)),
			// now on stack: Q (1,true)
			Dup(),
			LoadField(valTy, subClass, "Value")
		];
		comparison :=  <ruleComparison-to-cil> (<nabl2-get-ast-type>pat, pat, noMatch, context); 
		result := <concat>[
			q, 
			comparison,
			[Pop(), Br(LabelRef(match)), // Pop the Union class 
			Label(noMatch), Pop(), Br(LabelRef(endOfRule)), Label(match)] // pop the value within the union
			]
	

	
	namedWildcard-to-cil: (_, a@Identifier(name), endOfRule, context@(func,wildcards)) -> 
		[StoreLocal(<getIdentifierIndex; int-to-string> (wildcards, a))]
			
	typeConstIdentifier-to-cil: (ty@UnionType(x), lhs@Identifier(subtype), endOfRule, context@(func,wildcards)) -> result with
		// check whether  lhs is of same subtype
		baseTy := ClassType(DefaultModule(), ClassName(<concat-strings> ["Test1/", x]), NonGeneric());
		subClass := ClassType(DefaultModule(), ClassName(<concat-strings> ["Test1/", subtype]), NonGeneric());
		result := [
			Dup(),
			Call(Static(), IntType(), subClass, "GetTag", None(), []),
			Call(Instance(), BoolType(), baseTy, "EqualTypes", None(), [IntType()]),
			BrFalse(LabelRef(endOfRule)),
			Pop()
		]
	
	// Caller pushes complete tuple on stack. This function always leaves the tuple on stack, regardless of whether or not it matches
	compareTupleElem: (elemTy, lhsElem, idx, tupleTy, lhs, endOfRule, context) ->  result with
		noMatch := <newname> "tupleElem_nomatch_";
		match   := <newname> "tupleElem_match_";
		getItem := [Call(Instance(), <concat-strings>["!", <int-to-string>idx], <type-to-cil> tupleTy, <concat-strings>["get_Item", <add;int-to-string> (1,idx)], None(), [])];
		a       := <ruleComparison-to-cil> (elemTy, lhsElem, noMatch, context);
		result  := <concat>[
			[Dup()], // duplicate tuple 
			getItem, // consumes tuple
			a,
			[Br(match), Label(noMatch), Pop(), // pop elem 
			 Br(endOfRule), Label(match)]
			]
		
	
	compareListItems: (EmptyPatternList(), rest) -> [Pop()]
	compareListItems: (PatternConcat(a,b), (ty@List(x), endOfRule, context@(_,wildcards))) -> result with 
		noMatch := <newname> "compareItems_nomatch_";
		match   := <newname> "compareItems_match_";
		result  := <concat>[
			[Dup(), Call(Instance(), "!0", <type-to-cil> ty, "get_HeadOrDefault", None(), [])], 
			<ruleComparison-to-cil> (x, a, noMatch, context), 
			[Dup()],
			<getListTail> ty,
			<compareListItems> (b, (ty, noMatch, context)),
			[Br(match), 
			   Label(noMatch), Pop(), // pop head/tail
			   Br(endOfRule), Label(match), Pop()]
			]
	compareListItems: (a, (ty, endOfRule, context)) -> <ruleComparison-to-cil> (ty, a, endOfRule, context)



	// Helper functions
	
	

	getListTail: ty -> result with
		result := [Call(Instance(), ClassType(Module("FSharp.Core"), ClassName("Microsoft.FSharp.Collections.FSharpList"), GenericClassTypeArgs("1", [ArgType1("0")])),
			<type-to-cil> ty, "get_TailOrNull", None(), [])]
		
	endsWithWildcard: PatternConcat(a,b) -> <endsWithWildcard> b
	endsWithWildcard: a@Identifier(_) -> a
	endsWithWildcard: a@Wildcard() -> a
	endsWithWildcard: _ -> <fail>
	
	getPatternListLength: PatternConcat(a,b) -> <add> (1, <getPatternListLength> b) 
	getPatternListLength: EmptyPatternList() -> 0
	getPatternListLength: Identifier(_) -> 0
	getPatternListLength: Wildcard() -> 0	
	
	eq-ast: (a,a) -> [a]
	eq-ast: (a,b) -> []
	isNamedWildcard: a@Identifier(x) -> result with
		result := <eq-ast> (a, <getOrigin> a)
	getNamedWildcars: x -> < collect-all(?Identifier(_));  mapconcat(isNamedWildcard)> x
	