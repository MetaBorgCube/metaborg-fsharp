module compile

imports

  libstratego-gpp
  libspoofax/stratego/debug
  libstratego-lib
  src-gen/signatures/-
  nabl2/api
  trans/pp
  trans/desugar
  trans/minifsharp
  src-gen/nabl2/static-semantics
  trans/compile-pattern-matching
  
strategies
	
	external strategy-generate-cil(|path)
	external strategy-run-cil(|path)

signature constructors
	
	Output: String * String -> Term
	
rules

	
	generate-cil: (selected, position, ast, path, project-path) -> (filename, result) with
		filename 	:= <guarantee-extension(|"result.log")> path;
		(cname, _) 	:= <show-cil> (selected, position, ast, path, project-path);
		result		:= <strategy-generate-cil(|path); process-output> cname
		
	run-cil: (selected, position, ast, path, project-path) -> (filename, result) with
		filename 	:= <guarantee-extension(|"result.txt")> path;
		(cname, _) 	:= <show-cil> (selected, position, ast, path, project-path);
		result		:= <strategy-run-cil(|path); process-output> cname
	
	show-cil: (selected, _, ast, path, project-path) -> (filename, result) with
	    filename := <guarantee-extension(|$[cil])> path;
	      a         := <nabl2-get-ast-analysis> ast;
		result := <desugar-all; program-to-cil;  pp-debug> ast

	program-to-cil: Program(e,l@Let(_,a,b,c,d)) -> result with
		wildcardVars := <getNamedWildcars;map(nabl2-get-ast-type; strip-forall; type-to-cil)> d; 
		result := ProgramCil(<concat> [[Method(IntType(), a, None(), [], LocalDefs(wildcardVars), <program-to-cil> (d, (l, [])))], <nonEntryPoints-to-cil> e], a)
		 
	nonEntryPoints-to-cil: None() -> []
	nonEntryPoints-to-cil: Some(x) -> <listifyExprBlock; map(let-to-cil); concat> x
	
	process-output: Output(result, "") -> result

	process-output: Output(result, error) -> $[[result] Runtime error: [error]]
		where not (<""> error)
	
// Application	
	
	// first application: a is an identifier, hence need to call constructor with free vars as parameters
	program-to-cil: (z@Application(a@Identifier(name),b), context) -> result with
		funTy := <nabl2-get-ast-type;strip-forall> a;
		node         := <nabl2-get-ast-analysis> name;
		ref-occ      := <nabl2-mk-occurrence(|"Funcs")> name;
		dec-occ      := <nabl2-get-resolved-name(|node); Fst> ref-occ;
		targetFunc   := <nabl2-get-type(|node)> dec-occ;
		freeVars     := <getFreeVars> ([], targetFunc);
		freeVarTys   := <map(nabl2-get-ast-type)> freeVars;
		loadFreeVars := <mapconcat(\x->(x, context)\;program-to-cil)> freeVars;
		callCtor     := [NewObj(ClassType(DefaultModule(), ClassName(<concat-strings>["Test1/", name, "_0"]), NonGeneric()), <map(type-to-cil)> freeVarTys)];
		loadArg      := <program-to-cil> (b, context);
		invokeFunc   := [CallVirt(<type-to-cil> funTy)];
		result       :=  <concat> [
			loadFreeVars, 
			callCtor, 
			loadArg, 
			invokeFunc
		]
	
	// not the first application: a is not an identifier, hence no need to call constructor with free vars as parameters
	program-to-cil: (z@Application(a,b), context) -> result with
		funTy := <nabl2-get-ast-type;strip-forall> a;
		result :=  <concat> [<program-to-cil> (a, context), <program-to-cil> (b, context), [CallVirt(<type-to-cil> funTy)]]
	

// Type related stuff	
	
	type-to-cil: Forall(_, y)  -> <type-to-cil> y
	type-to-cil: Function(x,y) -> ClassType(Module("FSharp.Core"), ClassName("Microsoft.FSharp.Core.FSharpFunc"), GenericClassTypeArgs("2", [<type-to-cil> x,<type-to-cil> y]))
	type-to-cil: Int()         -> IntType()
	type-to-cil: Bool()        -> BoolType()
	type-to-cil: Tuple(x)      -> ClassType(Module("mscorlib"), ClassName("System.Tuple"), GenericClassTypeArgs(<length;int-to-string> x, <map(type-to-cil)> x))
	type-to-cil: List(x)       -> ClassType(Module("FSharp.Core"), ClassName("Microsoft.FSharp.Collections.FSharpList"), GenericClassTypeArgs("1", [<type-to-cil> x]))
	type-to-cil: TVar(x)       -> <concat-strings> ["!!", x]
	
	arg-type-to-cil: (Forall(_,x), argName, args) -> <arg-type-to-cil> (x, argName, args)
	arg-type-to-cil: (Function(x,y), argName, [ArgIdentifier(argName) | _]) -> <type-to-cil> x
	arg-type-to-cil: (Function(x,y), argName, [TypedArgIdentifier(argName,_) | _]) -> <type-to-cil> x
	arg-type-to-cil: (Function(x,y), argName, [_ | argTl]) -> <arg-type-to-cil> (y, argName, argTl)
	
	ret-type-to-cil: Function(x,y) -> <ret-type-to-cil> y
	ret-type-to-cil: x -> <type-to-cil> x
	
	sort-typevars: x -> <sort-list(LSort(\(a,b) -> <gtS>(<string-replace(|"T","")> a, <string-replace(|"T","")> b)\))> x
	getGenericVarList: x -> <collect-all(?TVar(_));map(\TVar(z) -> z\); sort-typevars> x
	genericVarListToOption: [] -> None()
	genericVarListToOption: x -> Some(GenericTypeIndicator(x))
	getOptionalGenericVars: x -> <getGenericVarList; genericVarListToOption> x
	
	strip-forall: Forall(x,y) -> y
	strip-forall: x -> x
	
// Misc cil-generation

	program-to-cil: (ExprBlock(exp, None()), context) -> <program-to-cil> (exp, context)
	program-to-cil: (ExprBlock(exp, Some(next)), context) -> <concat> [<program-to-cil> (exp, context), <program-to-cil> (next, context)]
	
	program-to-cil: (Const(Integer(x)), context) -> [Ldc(x)]
	program-to-cil: (Const(Bool(True())), context) -> [Ldc("1")]
	program-to-cil: (Const(Bool(False())), context) -> [Ldc("0")]
	
	program-to-cil: (BinaryOperation(op, a, b), context) -> <concat> [<program-to-cil> (a, context), <program-to-cil> (b, context), <binop-to-cil> op]
	binop-to-cil: PlusOp()    -> [Add()]
	binop-to-cil: MinOp()     -> [Sub()]
	binop-to-cil: MultOp()    -> [Mul()]
	binop-to-cil: EqualOp()   -> [Ceq()]
	binop-to-cil: SmallerOp() -> [Clt()]	
	binop-to-cil: OrOp()      -> [Or()]  // Note: bitwise or.  Should be OK since bools are always 0 or 1
	binop-to-cil: AndOp()     -> [And()] // Note: bitwise and. Should be OK since bools are always 0 or 1	
	
	program-to-cil: (v@Identifier(name), context) -> <getVar> (v, context)
		
	program-to-cil: (Let(_,_,_,_,_), context) -> [] // functions are compiled elsewhere; don't try to compile them into other function bodies
		
	program-to-cil: (c@ExpTuple(l), context) -> result with
		items := <mapconcat(\x -> <program-to-cil> (x, context)\)> l;
		paramTys := <length; range; map(\x -> ArgType1(<int-to-string>x)\)> l;
		ctor := [NewObj(<nabl2-get-ast-type;type-to-cil> c, paramTys)];
		result := <concat> [items, ctor]
		
	program-to-cil: (c@Concat(a,b), context) -> result with
		cilA := <program-to-cil> (a, context);
		cilB := <program-to-cil> (b, context);
		cilClassTy := ClassType(Module("FSharp.Core"), ClassName("Microsoft.FSharp.Collections.FSharpList"), GenericClassTypeArgs("1", [ArgType1("0")]));
		call := Call(Static(), cilClassTy, <nabl2-get-ast-type;type-to-cil>c, "Cons", None(), [ArgType1("0"), cilClassTy]);
		result := <concat> [cilA, cilB, [call]] 
	program-to-cil: (c@EmptyList(), context) -> result with
		cilClassTy := ClassType(Module("FSharp.Core"), ClassName("Microsoft.FSharp.Collections.FSharpList"), GenericClassTypeArgs("1", [ArgType1("0")]));
		result := [Call(Static(), cilClassTy, <nabl2-get-ast-type;type-to-cil>c, "get_Empty", None(), [])]

		
		
		
	getLetArgs: Let   (_,_, args, _, _) -> args


	getVar: (identifier, (func, wildcardDefs)) -> result with
		wildcardIdx := <getIdentifierIndex> (wildcardDefs, identifier);
		if <lt> (wildcardIdx, 0) then 
			result := [LoadArg(<getArg;int-to-string> (identifier, func))]
		else 
			result := [LoadLocal(<int-to-string> wildcardIdx)] 
		end
	// returns index of the identifier in the sequence [param1, ..., paramn, freeVar1, ..., freeVarn] 
	getArg: (identifier, func) -> result with
		freeVars   := <getFreeVars> ([], func);
		freeVarIdx := <getIdentifierIndex> (freeVars, identifier);
		paramIndex := <getIdentifierIndex> (<getLetArgs>func, identifier);
		numParams  := <getLetArgs;length> func;
		if <lt> (freeVarIdx, 0) then result := paramIndex else  result := <add> (numParams, freeVarIdx) end

	let-to-cil: a @ Let(recursiveType, name,args,retTy,body) -> result with 
		tmp 	   	 := <nabl2-get-ast-type; strip-forall> a;
		returnType 	 := <ret-type-to-cil> tmp;
		argTypes 	 := <map(\x -> <arg-type-to-cil> (tmp, <getIdentifierName> x, args)\)> args;
		genericTypes := <getOptionalGenericVars> tmp;
		freeVars 	 := <getFreeVars> ([], a);
		freeVarTypes := <map(nabl2-get-ast-type; strip-forall; type-to-cil)> freeVars;
		allArgTypes  := <concat>  [argTypes, freeVarTypes];
		wildcardVars := <getNamedWildcars;map(nabl2-get-ast-type; strip-forall; type-to-cil)> body; 
		method 		 := Method(returnType, name, genericTypes, allArgTypes, LocalDefs(wildcardVars), <program-to-cil> (body, (a, [])));
		curriedFunc  := <curriedFunction-to-cil> (tmp, tmp, name, [], args, freeVars);
		result 	 	 := <concat> [[method], curriedFunc, <listifyExprBlock;mapconcat(let-to-cil)> body]
	let-to-cil: _ -> []


 
	getIdentifierName: ArgIdentifier(x) -> x
	getIdentifierName: TypedArgIdentifier(x, _) -> x
	getIdentifierName: Identifier(x) -> x
	getIdentifierName: Let(_, name, _, _, _)    -> name // required for getExprBlockArgs (bit of a dirty hack..)
	getIdentifierName: LetConst(name, _, _)  -> name // required for getExprBlockArgs
	
	
	curriedFunction-to-cil: (_, _, _, _, [], freeVars) -> []
	curriedFunction-to-cil: (funTy, ty@Function(x,y), name, processedArgs, nextArgs@[argHd | argTl], freeVars) -> result with
		funcName 	 := <concat-strings> [name, "_", <length;int-to-string>processedArgs];
		baseClass 	 := <type-to-cil> ty;
		argFields    := <map(generateField)> processedArgs;
		freeVarFields:= <map(generateField; \Field(a,b) -> Field(a, <getFreeVarName> b)\)> freeVars;
		allFields    := <concat> [argFields, freeVarFields];
		ctor 		 := <generateConstructor> (funcName, processedArgs, baseClass, freeVars);
		retTy 		 := <type-to-cil> y;
		argTy        := <type-to-cil> x;
		instructions := <generateInvokeImplementation> (funTy, name, processedArgs, nextArgs, freeVars);
		genericVars  := <getOptionalGenericVars> funTy;
		function     := FunctionDefinition(funcName, genericVars, baseClass, allFields, ctor, retTy, argTy, instructions);
		nextFunction := <curriedFunction-to-cil> (funTy, y, name, <concat> [processedArgs, [argHd]], argTl, freeVars);
		result 		 := <concat> [[function], nextFunction] 
		
		
	loadArg: (processed, funTy, className, identifierName) -> result with 
		fldType := <arg-type-to-cil> (funTy,identifierName, processed);
		result := [LoadArg("0"), LoadField(fldType, className, identifierName)] 
	loadArgs: (processed, funTy, className) -> <mapconcat(\x -> <loadArg> (processed, funTy, className, <getIdentifierName> x)\)> processed

	getFreeVarName: x -> <concat-strings> ["freevar_", x]

	loadFreeVar: (className, freeVar) -> result with
		ty        := <nabl2-get-ast-type; type-to-cil> freeVar;
		fieldName := <getIdentifierName;getFreeVarName> freeVar;
		result    := [LoadArg("0"), LoadField(ty, className, fieldName)]
	loadFreeVars: (className, freeVars) -> result with
		result := <mapconcat(\x -> (className, x)\; loadFreeVar)> freeVars
	
	getCurrentArgs: (processed, []) -> processed
	getCurrentArgs: (processed, [hd|tl]) -> <concat> [processed, [hd]]
	
	generateInvokeImplementation: (funTy, className, processed, nextArgs, freeVars) -> result with
		objName         := <concat-strings> [className, "_", <length;int-to-string> processed];
		loadCurriedArgs := <loadArgs> (processed, funTy, objName);
		loadFreeVars    := <loadFreeVars> (<concat-strings> [className, "_", <length;int-to-string> processed], freeVars);
		argTys          := <getCurrentArgs;map(nabl2-get-ast-type;type-to-cil)> (processed, nextArgs);       // Note: does not work for generics
		freeVarTys      := <map(nabl2-get-ast-type;type-to-cil)> freeVars;
		tys             := <concat> [argTys, freeVarTys];
		if <lt> (<length>nextArgs, 2) then 
			funcName    := <concat-strings> ["get_", className];
			callNext    := [Call(Static(), <ret-type-to-cil> funTy, ClassType(DefaultModule(), ClassName("Test1"), NonGeneric()), funcName, None(), tys), Ret()] 
		else 
			callNext    := [NewObj(ClassType(DefaultModule(), ClassName(<concat-strings>["Test1/", className, "_", <addS> ("1", <length;int-to-string> processed)]), NonGeneric()), tys)]
		end;
		result          := <concat> [
			loadCurriedArgs,
			[LoadArg("1")],
			loadFreeVars,
			callNext
	 	]
 

 
 	// a field is a param, but named differently..
	generateField: x -> <generateParam; \Param(a,b) -> Field(a,b)\> x

	generateFieldInit: (idx, className, []) -> []
	generateFieldInit: (idx, className, [Param(ty,name) | tl]) -> <concat>[
		[
			LoadArg("0"),
			LoadArg(idx),
			StoreField(ty, className, name)
		],
		<generateFieldInit> (<addS>(idx, "1"), className, tl)
		]

	generateBaseInit: baseType -> <concat>[[LoadArg("0")], [CallConstructor(baseType)]]
	generateParam: x -> result with
		ty     := <nabl2-get-ast-type;strip-forall;type-to-cil> x;
		name   := <getIdentifierName> x;
		result := Param(ty, name)
	generateConstructor: (className, params, baseType, freeVars) -> result with 
		curried_params := <map(generateParam)> params;
		freeVar_params := <map(generateParam; \Param(ty,name) -> Param(ty, <getFreeVarName>name)\)> freeVars;
		all_params     := <concat> [curried_params, freeVar_params];
		body 		   := <concat> 
		[
			<generateBaseInit> baseType,
			<generateFieldInit> ("1", className, all_params),
			[Ret()]
	    ];
		result := Ctor(all_params, body)

	


	
	
	
	listifyExprBlock: ExprBlock(exp, Some(next)) -> <concat> [[exp], <listifyExprBlock> next]
	listifyExprBlock: ExprBlock(exp, None()) -> [exp]
	
	
	
	getFreeVars: (scope, None()) -> [] 
	getFreeVars: (scope, Some(x)) -> <getFreeVars> (scope, x)
	getFreeVars: (scope, ExprBlock(exp, tail)) -> result with  
		args   := <getExprBlockArgs> (scope, exp);
		result := <concat> [<getFreeVars> (scope, exp), <getFreeVars> (<concat> [scope, args], tail)]
	getFreeVars: (scope, BinaryOperation(op, a, b)) -> <concat> [<getFreeVars> (scope, a), <getFreeVars> (scope, b)]
	getFreeVars: (scope, Const(_)) -> []
	
	getFreeVars: (scope, x@Identifier(_), None()) -> <getFreeVar> (scope, x)
	getFreeVars: (scope, x@Identifier(_), Some(func@Let(_,_,_,_,_))) -> <getFreeVars> (scope, func)
	getFreeVars: (scope, x@Identifier(name)) -> result with 
		node       := <nabl2-get-ast-analysis> name;
		ref-occ    := <nabl2-mk-occurrence(|"Funcs")> name;
		dec-occ    := <nabl2-get-resolved-name(|node); Fst> ref-occ;
		targetFunc := <nabl2-get-type(|node)> dec-occ;
		result     := <getFreeVars> (scope, x, targetFunc)
		
		
	getFreeVars: (scope, Application(a,b)) -> <concat> [<getFreeVars> (scope, a), <getFreeVars> (scope, b)]
	
	
	getFreeVars: (scope, Let(_,name,args,retTy,body)) -> result with
		newscope := <concat> [args, scope];
		result   := <getFreeVars> (newscope, body)
	
	getFreeVars: (scope, Match(_,_)) -> [] // TODO

	getExprBlockArgs: (scope, l@Let(_,_, _, _, _)) -> [l]
	getExprBlockArgs: (scope, l@LetConst(_, _, _)) -> [l]
	getExprBlockArgs: (scope, _) -> []
	
	

	// helpers for getIdentifierIndex.
	indexOf: ([]        , var, idx) -> -1
	indexOf: ([var | tl], var, idx) -> idx
	indexOf: ([_   | tl], var, idx) -> <indexOf> (tl, var, <add> (idx, 1))
	
	getOrigin: Identifier(var_name) -> result with 
		node    := <nabl2-get-ast-analysis> var_name;
		ref-occ := <nabl2-mk-occurrence(|"Const")> var_name;
		dec-occ := <nabl2-get-resolved-name(|node);Fst> ref-occ;
		result  := <nabl2-get-property(|node, "origin")> dec-occ
	
	// gets origin if it is an identifier, otherwise returns the identity
	getWeakOrigin: a@Identifier(_) -> <getOrigin> a
	getWeakOrigin: x -> x
	
	// returns the index of var in the list vars, or -1 if not present. Uses only the name in the identifier.
	getIdentifierIndex: (vars, var) -> result with
		varOrigins := <map(getWeakOrigin)> vars;
		varOrigin  := <getOrigin> var;
		result 	:= <indexOf> (varOrigins, varOrigin, 0)
		
	getFreeVar: (vars, var) -> result with 
		varOrigin := <getOrigin> var;
		if <elem> (varOrigin, vars) then result := [] else result := [var] end
 
		
